import com.a65apps.changelog.LogOrder

plugins {
    id 'com.a65apps.changelog'
    id 'io.gitlab.arturbosch.detekt' version '1.19.0'
}

repositories {
    mavenCentral()
}

changelog {
    def token = System.getenv().get("TOKEN")

    currentVersion = "1.1.10"
    currentReleaseBranch = "releases/7"
    lastReleaseBranch = "releases/6"
    templateFile = "template/changelog.mustache"
    accessToken = token
    order = LogOrder.LAST_TO_FIRST
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.19.0"
}

detekt {
    failFast = true
    buildUponDefaultConfig = true
    input = files("plugin/src/main/kotlin")

    reports {
        html.enabled = true
        xml.enabled = true
        txt.enabled = true
    }
}

tasks.detekt.jvmTarget = "1.8"

tasks.register('test') {
    dependsOn gradle.includedBuild('plugin').task(':test')
}

tasks.register('publishPlugins') {
    dependsOn gradle.includedBuild('plugin').task(':publishPlugins')
}

tasks.register('publish') {
    dependsOn gradle.includedBuild('plugin').task(':publish')
}
